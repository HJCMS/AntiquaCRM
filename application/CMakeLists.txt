# -*- coding: utf-8 -*-
# vim: set fileencoding=utf-8

CMAKE_MINIMUM_REQUIRED(VERSION 3.5)

###
##  \Section project main info
###
SET (PROJECTNAME "antiquacrm")
SET (DISPLAYNAME "Antiqua CRM")
SET (DESCRIPTION "Application for Managing antiquarian Books")
SET (HOMEPAGEURL "https://www.hjcms.de")
SET (ANTIQUACRM_VERSION_MAJOR 1)
SET (ANTIQUACRM_VERSION_MINOR 0)
SET (ANTIQUACRM_VERSION_RELEASE 7)
SET (ANTIQUACRM_VERSION_STRING "${ANTIQUACRM_VERSION_MAJOR}.${ANTIQUACRM_VERSION_MINOR}.${ANTIQUACRM_VERSION_RELEASE}")
## @see src/antiquacrm.rc
SET (ANTIQUACRM_VERSION_FILEOS "${ANTIQUACRM_VERSION_MAJOR},${ANTIQUACRM_VERSION_MINOR},${ANTIQUACRM_VERSION_RELEASE},0")
### Required for Package building
## https://www.rfc-editor.org/rfc/rfc4122.html#section-4.3
SET (RFC4122_DNS_NAMESPACE 6ba7b810-9dad-11d1-80b4-00c04fd430c8)
STRING (UUID ANTIQUACRM_DNSID NAMESPACE ${RFC4122_DNS_NAMESPACE} NAME "hjcms.de" TYPE SHA1 UPPER)
SET (RFC4122_URL_NAMESPACE 6ba7b811-9dad-11d1-80b4-00c04fd430c8)
STRING (UUID ANTIQUACRM_URLID NAMESPACE ${RFC4122_URL_NAMESPACE} NAME "https://hjcms.de" TYPE SHA1 UPPER)
SET (RFC4122_ISO_NAMESPACE 6ba7b812-9dad-11d1-80b4-00c04fd430c8)
STRING (UUID ANTIQUACRM_APPID NAMESPACE ${RFC4122_ISO_NAMESPACE} NAME "antiquacrm" TYPE SHA1 UPPER)

PROJECT(${PROJECTNAME} 
  VERSION ${ANTIQUACRM_VERSION_MAJOR}.${ANTIQUACRM_VERSION_MINOR}.${ANTIQUACRM_VERSION_RELEASE}
  DESCRIPTION ${DESCRIPTION}
  HOMEPAGE_URL ${HOMEPAGEURL}
  LANGUAGES CXX
)

###
## \Section Build System
###
SET(CMAKE_AUTOUIC OFF)
SET(CMAKE_AUTOMOC ON)
SET(CMAKE_AUTORCC ON)
SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_INCLUDE_CURRENT_DIR ON)
SET(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/Modules")
SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -W -Wall -Wno-implicit-fallthrough -Werror")

IF (CMAKE_CXX_COMPILER MATCHES ".*clang")
  SET (CMAKE_COMPILER_IS_CLANGXX 1)
## SET (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-uninitialized")
ENDIF ()

## https://ccache.dev/
FIND_PROGRAM (CCACHE_EXECUTABLE NAMES ccache)
IF (CCACHE_EXECUTABLE)
  SET_PROPERTY (GLOBAL PROPERTY RULE_LAUNCH_COMPILE ${CCACHE_EXECUTABLE})
  SET_PROPERTY (GLOBAL PROPERTY RULE_LAUNCH_LINK ${CCACHE_EXECUTABLE})
ENDIF ()

## Enables some Development options and tracing
OPTION (ANTIQUA_DEVELOPEMENT "Enable development macro code" OFF)
IF (ANTIQUA_DEVELOPEMENT)
 ADD_DEFINITIONS (-DANTIQUA_DEVELOPEMENT)
 SET (ANTIQUACRM_DATA_TARGET "${CMAKE_CURRENT_SOURCE_DIR}/src/data")
 SET (ANTIQUACRM_PLUGIN_TARGET "${CMAKE_CURRENT_BINARY_DIR}/src/plugins/antiquacrm")
 SET (ANTIQUACRM_TRANSLATION_TARGET "${CMAKE_CURRENT_BINARY_DIR}/src/i18n")
ELSE(ANTIQUA_DEVELOPEMENT)
 IF (CMAKE_HOST_WIN32)
  SET (ANTIQUACRM_DATA_TARGET "data")
  SET (ANTIQUACRM_PLUGIN_TARGET "plugins/antiquacrm")
  SET (ANTIQUACRM_TRANSLATION_TARGET "i18n")
 ELSE (CMAKE_HOST_WIN32)
  SET (ANTIQUACRM_LIB_SUFFIX "64")
  SET (ANTIQUACRM_DATA_TARGET "${CMAKE_INSTALL_PREFIX}/share/${PROJECTNAME}/data")
  SET (ANTIQUACRM_PLUGIN_TARGET "${CMAKE_INSTALL_PREFIX}/lib${ANTIQUACRM_LIB_SUFFIX}/antiquacrm/plugins")
  SET (ANTIQUACRM_TRANSLATION_TARGET "${CMAKE_INSTALL_PREFIX}/share/${PROJECTNAME}/i18n")
 ENDIF (CMAKE_HOST_WIN32)
ENDIF (ANTIQUA_DEVELOPEMENT)

## Zlib
FIND_PACKAGE(ZLIB REQUIRED)
IF (NOT ZLIB_FOUND)
 MESSAGE (FATAL_ERROR "AntiquaCRM: Requires gzip compression for Network connections!")
ENDIF (NOT ZLIB_FOUND)

## libqrencode
## https://github.com/fukuchi/libqrencode
FIND_PACKAGE(QRENCODE REQUIRED)
IF (QRENCODE_FOUND)
 MESSAGE (STATUS "AntiquaCRM: Bookcard printing enabled!")
ELSE (QRENCODE_FOUND)
 MESSAGE (FATAL_ERROR "AntiquaCRM: Requires libqrencode for Bookcard printing!")
ENDIF (QRENCODE_FOUND)

## cdio
FIND_PACKAGE(CDIO)
IF (CDIO_FOUND)
 FIND_PACKAGE(CDDB)
 IF (CDDB_FOUND)
  MESSAGE (STATUS "AntiquaCRM: CD Tracking and CDDB support enabled!")
  SET (CDTRACKING_SUPPORT ON)
  SET (CDTRACKING_LIBRARIES ${CDIO_LIBRARIES} ${CDDB_LIBRARIES})
 ENDIF (CDDB_FOUND)
ENDIF (CDIO_FOUND)

SET(PROJECT_BINDINGS
 Core
 Widgets
 Sql
 Network
 Xml
 Charts
 LinguistTools
 PrintSupport
)

IF (CMAKE_HOST_UNIX)
 FIND_PACKAGE(X11)
  IF(X11_FOUND)
   MESSAGE (STATUS "AntiquaCRM: X11-DBus Support enabled!")
   SET(PROJECT_BINDINGS
    ${PROJECT_BINDINGS}
    X11Extras
    DBus
   )
 ENDIF(X11_FOUND)
ENDIF (CMAKE_HOST_UNIX)

IF (CMAKE_HOST_WIN32)
  SET(PROJECT_BINDINGS
   ${PROJECT_BINDINGS}
   WinExtras
  )
ENDIF (CMAKE_HOST_WIN32)

FIND_PACKAGE(QT NAMES Qt5 REQUIRED COMPONENTS ${PROJECT_BINDINGS})
FIND_PACKAGE(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS ${PROJECT_BINDINGS})

## Required by src/i18n and QT5_CREATE_TRANSLATION
SET (LUPDATE_SCANDIRS
  ${CMAKE_CURRENT_BINARY_DIR}/src
)

SET (ANTIQUABUILD_SOURCE_DIR
  ${CMAKE_CURRENT_SOURCE_DIR}/src
)

###
## \Section Headers
## BUG Qt5 don't add automaticly Network,Sql,Xml Header Paths :(
###

SET (PROJECT_INCLUDE_DIRS
  ${CMAKE_CURRENT_BINARY_DIR}/src
  ${ANTIQUABUILD_SOURCE_DIR}
  ${Qt${QT_VERSION_MAJOR}Network_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}Sql_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}Xml_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}PrintSupport_INCLUDE_DIRS}
)

ADD_SUBDIRECTORY (xdg)
ADD_SUBDIRECTORY (src)

###
## Configure Inno Setup file
###
IF (CMAKE_HOST_WIN32)
 CONFIGURE_FILE (antiquacrm.iss.cmake ${CMAKE_CURRENT_BINARY_DIR}/antiquacrm.iss)
ENDIF (CMAKE_HOST_WIN32)

##EOF
