# -*- coding: utf-8 -*-
# vim: set fileencoding=utf-8

CMAKE_MINIMUM_REQUIRED(VERSION 3.5)

###
##  \Section project main info
###
SET (PROJECTNAME "antiquacrm")
SET (DISPLAYNAME "Antiqua CRM")
SET (DESCRIPTION "Application for Managing antiquarian Books")
SET (HOMEPAGEURL "https://www.hjcms.de")
SET (ANTIQUACRM_VERSION_MAJOR 1)
SET (ANTIQUACRM_VERSION_MINOR 0)
SET (ANTIQUACRM_VERSION_RELEASE 0)
SET (ANTIQUACRM_VERSION_STRING "${ANTIQUACRM_VERSION_MAJOR}.${ANTIQUACRM_VERSION_MINOR}.${ANTIQUACRM_VERSION_RELEASE}")
SET (ANTIQUACRM_VERSION_FILEOS "${ANTIQUACRM_VERSION_MAJOR},${ANTIQUACRM_VERSION_MINOR},${ANTIQUACRM_VERSION_RELEASE},0")

PROJECT(${PROJECTNAME} 
  VERSION ${ANTIQUACRM_VERSION_MAJOR}.${ANTIQUACRM_VERSION_MINOR}.${ANTIQUACRM_VERSION_RELEASE}
  DESCRIPTION ${DESCRIPTION}
  HOMEPAGE_URL ${HOMEPAGEURL}
  LANGUAGES CXX
)

###
## \Section Build System
###
SET(CMAKE_AUTOUIC OFF)
SET(CMAKE_AUTOMOC ON)
SET(CMAKE_AUTORCC ON)
SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_INCLUDE_CURRENT_DIR ON)
SET(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/Modules")
SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -W -Wall -Wno-unused-function -Wno-implicit-fallthrough -Werror")

## Enables some Development options and tracing
OPTION (ANTIQUA_DEVELOPEMENT "Enable development macro code" OFF)
IF (ANTIQUA_DEVELOPEMENT)
 ADD_DEFINITIONS (-DANTIQUA_DEVELOPEMENT)
 SET (ANTIQUACRM_DATA_TARGET "${CMAKE_CURRENT_SOURCE_DIR}/src/data")
 SET (ANTIQUACRM_PLUGIN_TARGET "${CMAKE_CURRENT_BINARY_DIR}/src/plugins/antiquacrm")
 SET (ANTIQUACRM_TRANSLATION_TARGET "${CMAKE_CURRENT_BINARY_DIR}/src/i18n")
ELSE(ANTIQUA_DEVELOPEMENT)
 IF (CMAKE_HOST_WIN32)
  SET (ANTIQUACRM_DATA_TARGET "./data")
  SET (ANTIQUACRM_PLUGIN_TARGET "./plugins/antiquacrm")
  SET (ANTIQUACRM_TRANSLATION_TARGET "./i18n")
 ELSE (CMAKE_HOST_WIN32)
  SET (ANTIQUACRM_LIB_SUFFIX "64")
  SET (ANTIQUACRM_DATA_TARGET "${CMAKE_INSTALL_PREFIX}/share/${PROJECTNAME}/data")
  SET (ANTIQUACRM_PLUGIN_TARGET "${CMAKE_INSTALL_PREFIX}/lib${ANTIQUACRM_LIB_SUFFIX}/antiquacrm/plugins")
  SET (ANTIQUACRM_TRANSLATION_TARGET "${CMAKE_INSTALL_PREFIX}/share/${PROJECTNAME}/i18n")
 ENDIF (CMAKE_HOST_WIN32)
ENDIF (ANTIQUA_DEVELOPEMENT)

## Zlib
FIND_PACKAGE(ZLIB REQUIRED libzlib)
IF (NOT ZLIB_FOUND)
 MESSAGE (FATAL_ERROR "AntiquaCRM: Requires gzip compression for Network connections!")
ENDIF (NOT ZLIB_FOUND)

## libqrencode
## https://github.com/fukuchi/libqrencode
FIND_PACKAGE(QRENCODE REQUIRED libqrencode)

SET(PROJECT_BINDINGS
 Core
 Widgets
 Sql
 Network
 Xml
 LinguistTools
 PrintSupport
)

FIND_PACKAGE(QT NAMES Qt5 REQUIRED COMPONENTS ${PROJECT_BINDINGS})
FIND_PACKAGE(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS ${PROJECT_BINDINGS})

## Required by src/i18n and QT5_CREATE_TRANSLATION
SET (LUPDATE_SCANDIRS
  ${CMAKE_CURRENT_BINARY_DIR}/src
)

SET (ANTIQUABUILD_SOURCE_DIR
  ${CMAKE_CURRENT_SOURCE_DIR}/src
)

###
## \Section Headers
## BUG Qt5 don't add automaticly Network,Sql,Xml Header Paths :(
###

SET (PROJECT_INCLUDE_DIRS
  ${CMAKE_CURRENT_BINARY_DIR}/src
  ${ANTIQUABUILD_SOURCE_DIR}
  ${Qt${QT_VERSION_MAJOR}Network_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}Sql_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}Xml_INCLUDE_DIRS}
  ${Qt${QT_VERSION_MAJOR}PrintSupport_INCLUDE_DIRS}
)

ADD_SUBDIRECTORY (xdg)
ADD_SUBDIRECTORY (src)

##EOF
